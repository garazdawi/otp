#!/usr/bin/env escript
%% -*- erlang -*-
%%! +A0
%%
%% %CopyrightBegin%
%%
%% SPDX-License-Identifier: Apache-2.0
%%
%% Copyright Ericsson AB 2017-2026. All Rights Reserved.
%%
%% Licensed under the Apache License, Version 2.0 (the "License");
%% you may not use this file except in compliance with the License.
%% You may obtain a copy of the License at
%%
%%     http://www.apache.org/licenses/LICENSE-2.0
%%
%% Unless required by applicable law or agreed to in writing, software
%% distributed under the License is distributed on an "AS IS" BASIS,
%% WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
%% See the License for the specific language governing permissions and
%% limitations under the License.
%%
%% %CopyrightEnd%
%%

-mode(compile).

-compile([{nowarn_possibly_unsafe_function, {erlang, list_to_atom, 1}}]).

-record(cp, {name, class, dec, comp, cs, cat}).
-define(MOD, "unicode_util_gen").

main(Args) ->
    %%  Parse main table
    UD = file_open("../uc_spec/UnicodeData.txt"),
    Data0 = foldl(fun parse_unicode_data/2, [], UD),
    Data1  = array:from_orddict(lists:reverse(Data0)),
    ok = file:close(UD),

    %%  Special Casing table
    SC = file_open("../uc_spec/SpecialCasing.txt"),
    Data2 = foldl(fun parse_special_casing/2, Data1, SC),
    ok = file:close(SC),
    %%  Casing Folding table
    CF = file_open("../uc_spec/CaseFolding.txt"),
    Data = foldl(fun parse_case_folding/2, Data2, CF),
    ok = file:close(CF),

    %% Normalization
    ExclF = file_open("../uc_spec/CompositionExclusions.txt"),
    ExclData = foldl(fun parse_comp_excl/2, Data, ExclF),
    ok = file:close(ExclF),

    %%  GraphemeBreakProperty table
    Emoji = file_open("../uc_spec/emoji-data.txt"),
    Props00 = foldl(fun parse_properties/2, [], Emoji),
    %% Filter Extended_Pictographic class which we are interested in.
    Props0 = [EP || {extended_pictographic, _} = EP <- Props00],
    ok = file:close(Emoji),
    GBPF = file_open("../uc_spec/GraphemeBreakProperty.txt"),
    Props1 = foldl(fun parse_properties/2, Props0, GBPF),
    ok = file:close(GBPF),
    PropF = file_open("../uc_spec/PropList.txt"),
    Props2 = foldl(fun parse_properties/2, Props1, PropF),
    ok = file:close(PropF),
    Indic = file_open("../uc_spec/IndicSyllabicCategory.txt"),
    Props3 = foldl(fun parse_properties/2, Props2, Indic),
    ok = file:close(Indic),
    Props = sofs:to_external(sofs:relation_to_family(sofs:relation(Props3))),

    WidthF = file_open("../uc_spec/EastAsianWidth.txt"),
    WideCs = foldl(fun parse_widths/2, [], WidthF),
    ok = file:close(WidthF),

    %% Make module
    UpdateTests = case Args of
                      ["update_tests"] -> true;
                      _ -> false
                  end,

    {ok, Out} = file:open(?MOD++".hrl", [write]),
    gen_file(Out, Data, ExclData, maps:from_list(Props), WideCs, UpdateTests),
    ok = file:close(Out),
    ok.

file_open(File) ->
    {ok, Fd} = file:open(File, [read, raw, {read_ahead, 1000000}]),
    Fd.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

parse_unicode_data(Line0, Acc) ->
    Line = string:chomp(Line0),
    [CodePoint,Name0,Cat,Class,_BiDi,Decomp,
     _N1,_N2,_N3,_BDMirror,_Uni1,_Iso|Case] = tokens(Line, ";"),
    {Dec,Comp} = case to_decomp(Decomp) of
                     {_, _} = Compabil -> {[], Compabil};
                     Canon -> {Canon, []}
                 end,
    {Range, Name} = pick_range(Name0),
    case Range of
        last ->
            CP = #cp{name=list_to_binary(Name),class=to_class(Class),
                     dec=Dec, comp=Comp, cs=to_case(Case), cat=Cat},
            fill_range(Acc, CP, hex_to_int(CodePoint));
        _ ->
            [{hex_to_int(CodePoint),
              #cp{name=list_to_binary(Name),class=to_class(Class),
                  dec=Dec, comp=Comp, cs=to_case(Case), cat=Cat}}
            |Acc]
    end.


to_class(String) ->
    list_to_integer(string:trim(String, both)).

to_decomp("") -> [];
to_decomp("<" ++ Str) ->
    [Tag,Rest]  = string:lexemes(Str, ">"),
    {list_to_atom(Tag), to_decomp(Rest)};
to_decomp(CodePoints) ->
    CPL = string:lexemes(CodePoints, " "),
    [hex_to_int(CP) || CP <- CPL].

to_case(["","",""]) -> [];
to_case([Upper,Lower,Title]) ->
    {hex_to_int(Upper),hex_to_int(Lower),hex_to_int(Title),[]}.

pick_range([$<|Rest]) ->
    range_1(tokens(Rest, ","));
pick_range(Name) ->
    {false, Name}.

range_1([Name, " First>"]) ->
    {first, Name};
range_1([Name, " Last>"]) ->
    {last, Name};
range_1(Name) ->
    {false, lists:droplast(Name)}.

fill_range([{Start, CP}|_]=Acc, CP, Last) ->
    fill_range_1(Start+1, Last, CP, Acc).

fill_range_1(Start, Last, CP, Acc) when Start =< Last ->
    fill_range_1(Start+1, Last, CP, [{Start,CP}|Acc]);
fill_range_1(_Start, _Last, _CP, Acc) ->
    Acc.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

parse_special_casing("03A3;" ++ _, Acc) ->
    %% Break for conditional handling, which we don't support
    {done, Acc};
parse_special_casing(Line, Table) ->
    [CodePoint|CaseStr] = tokens(Line, ";"),
    CP = hex_to_int(CodePoint),
    Entry = array:get(CP, Table),
    Case = to_scase(CaseStr),
    array:set(CP, Entry#cp{cs=Case}, Table).

to_scase([Lower,Title,Upper|_]) ->
    {unlist([hex_to_int(CP) || CP <- string:lexemes(Upper, " ")]),
     unlist([hex_to_int(CP) || CP <- string:lexemes(Lower, " ")]),
     unlist([hex_to_int(CP) || CP <- string:lexemes(Title, " ")]),
     []}.

parse_case_folding(Line, Table) ->
    [CodePoint, Class0, CaseStr |_Comments] = tokens(Line, ";"),
    Class = string:trim(Class0, both),
    if Class =:= "T" -> Table; %% Do not support localization yet
       Class =:= "S" -> Table; %% Ignore simple
       true ->
            CP = hex_to_int(CodePoint),
            Case = unlist([hex_to_int(CPC) ||
                              CPC <- string:lexemes(CaseStr, " ")]),
            #cp{cs={U,L,T,_}} = Entry = array:get(CP, Table),
            array:set(CP, Entry#cp{cs={U,L,T,Case}}, Table)
    end.

unlist([A]) -> A;
unlist(A) -> A.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
parse_comp_excl(Line, Table) ->
    [CodePoint|_Comments] = tokens(Line, "#"),
    CP = hex_to_int(CodePoint),
    Entry = array:get(CP, Table),
    array:set(CP, Entry#cp{dec=none}, Table).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

parse_properties(Line0, Acc) ->
    [Line|_Comments] = tokens(Line0, "#"),
    [CodePoints, Class] = tokens(Line, ";"),
    case tokens(CodePoints, ".") of
        [CodePoint] ->
            [{to_atom(Class), {hex_to_int(CodePoint), undefined}}|Acc];
        [CodePoint1,"",CodePoint2] ->
            [{to_atom(Class), {hex_to_int(CodePoint1), hex_to_int(CodePoint2)}}|Acc]
    end.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% Pick ranges that are wide when seen from a non East Asian context,
%% That way we can minimize the data, every other code point is considered narrow.
%% We loose information but hopefully keep the important width for a standard
%% terminal.
parse_widths(Line0, Acc) ->
    [{WidthClass, {From, _To}=Range}] = parse_properties(Line0, []),
    case is_default_width(From, WidthClass) of
        {true, narrow} ->
            Acc;
        {false, narrow} ->
            [Range|Acc];
        {true, RuleRange} ->
            [RuleRange|Acc]
%%%     {false, rule_execption} ->  i.e. narrow codepoint in wide range
%%%        Should not happen in current specs
    end.

is_default_width(Index, WD) ->
    if
        16#3400 =< Index, Index =< 16#4DBF ->
            if WD =:= w orelse WD =:= f ->
                    {true, {16#3400, 16#4DBF}};
               true ->
                    {false, rule_execption}
            end;
        16#4E00 =< Index, Index =< 16#9FFF ->
            if WD =:= w orelse WD =:= f ->
                    {true, {16#4E00, 16#9FFF}};
               true ->
                    {false, rule_execption}
            end;
        16#F900 =< Index, Index =< 16#FAFF ->
            if WD =:= w orelse WD =:= f ->
                    {true, {16#F900, 16#FAFF}};
               true ->
                    {false, rule_execption}
            end;
        16#20000 =< Index, Index =< 16#2FFFD ->
            if WD =:= w orelse WD =:= f ->
                    {true, {16#20000, 16#2FFFD}};
               true ->
                    {false, rule_execption}
            end;
        16#30000 =< Index, Index =< 16#3FFFD ->
            if WD =:= w orelse WD =:= f ->
                    {true, {16#30000, 16#3FFFD}};
               true ->
                    {false, rule_execption}
            end;
        true ->
            {WD =:= n orelse WD =:= na orelse WD == h orelse WD =:= a, narrow}
    end.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

gen_file(Fd, Data, ExclData, Props, WideCs, UpdateTests) ->
    gen_header(Fd),    
    gen_props(Fd, Props, Data),
    gen_gc(Fd, Props),
    gen_compose_pairs(Fd, ExclData, Data),
    gen_case_table(Fd, Data),
    gen_unicode_table(Fd, Data, UpdateTests),
    gen_width_table(Fd, WideCs),
    ok.

gen_header(Fd) ->
    io:put_chars(Fd,"""
%%
%% this file is generated do not modify
%% see ../uc_spec/gen_unicode_mod.escript
"""),
   ok.

gen_props(Fd, Props, Data) ->
    PWS0 = maps:get(pattern_white_space, Props),
    PWS = merge_ranges(PWS0, split),
    io:put_chars(Fd, "%% Useful non-breakable whitespace chars\n"
                 "%% defined as Pattern White Space in Unicode Standard Annex #31\n"),
    io:put_chars(Fd, "-spec pattern_whitespace() -> [gc()].\n"),
    WsChars = [CP || {CP, undefined} <- PWS],
    io:format(Fd, "pattern_whitespace() -> ~w.\n\n", [[[$\r,$\n]|WsChars]]),

    WS0 = maps:get(white_space, Props),
    WS = merge_ranges(WS0, split),

    io:put_chars(Fd, "-spec is_whitespace(gc()) -> boolean().\n"),
    IsWS = fun(Range) -> io:format(Fd, "is_whitespace~s true;\n", [gen_single_clause(Range)]) end,
    io:format(Fd, "is_whitespace([13,10]) -> true;\n", []),
    [IsWS(CP) || CP <- WS],
    io:put_chars(Fd, "is_whitespace(_) -> false.\n\n"),

    OIDS = maps:get(other_id_start, Props),
    io:put_chars(Fd, "-spec is_other_id_start(gc()) -> boolean().\n"),
    IsODIS = fun(Range) -> io:format(Fd, "is_other_id_start~s true;\n", [gen_single_clause(Range)]) end,
    [IsODIS(CP) || CP <- merge_ranges(OIDS)],
    io:put_chars(Fd, "is_other_id_start(_) -> false.\n\n"),

    OICS = maps:get(other_id_continue, Props),
    io:put_chars(Fd, "-spec is_other_id_continue(gc()) -> boolean().\n"),
    IsOICS = fun(Range) -> io:format(Fd, "is_other_id_continue~s true;\n", [gen_single_clause(Range)]) end,
    [IsOICS(CP) || CP <- merge_ranges(OICS)],
    io:put_chars(Fd, "is_other_id_continue(_) -> false.\n\n"),

    PS0 = maps:get(pattern_syntax, Props),
    io:put_chars(Fd, "-spec is_letter_not_pattern_syntax(gc()) -> boolean().\n"),
    IsNLPS = fun(Range) -> io:format(Fd, "is_letter_not_pattern_syntax~s false;\n", [gen_single_clause(Range)]) end,
    KeepCat = fun(CP) ->
                      case array:get(CP, Data) of
                          #cp{cat = [$P,_]} -> false;
                          #cp{cat = [$S,_]} -> false;
                          #cp{cat = [$C,_]} -> false;
                          undefined -> false;
                          _ -> true
                      end
              end,
    PS = [{PSC, undefined} || {PSC, undefined} <:- split_ranges(PS0, []), KeepCat(PSC)],
    %% [io:format("~p ~p~n", [P, (array:get(P, Data))#cp.cat]) || {P,_} <- PS],
    [IsNLPS(CP) || CP <- merge_ranges(PS)],
    io:put_chars(Fd, "is_letter_not_pattern_syntax(_) -> true.\n\n"),

    ok.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

gen_gc(Fd, GBP) ->
    %% see http://www.unicode.org/reports/tr29/#Grapheme_Cluster_Boundary_Rules

    io:put_chars(Fd,
                 """

                 %% gc_1
                 gc_1(R0, $\r) ->
                      case cp(R0) of % Don't break CRLF
                          [$\n|R1] -> [[$\r,$\n]|R1];
                          _ -> [$\r|R0]
                      end;
                 %% Handle control

                 """),
    
    GenControl = fun(Range) -> io:format(Fd, "gc_1~s [CP|R0];\n", [gen_clause(Range)]) end,
    CRs0 = merge_ranges(maps:get(cr, GBP) ++ maps:get(lf, GBP) ++ maps:get(control, GBP), false),
    [R1,R2,R3|Crs] = CRs0,
    [GenControl(CP) || CP <- merge_ranges([R1,R2,R3], split), CP =/= {$\r, undefined}],
    %%GenControl(R1),GenControl(R2),GenControl(R3),
    io:put_chars(Fd, "\n%% Optimize Latin-1\n"),
    GenExtP = fun(Range) -> io:format(Fd, "gc_1~s gc_ext_pict(R0,[CP]);\n", [gen_clause(Range)]) end,
    ExtendedPictographic0 = merge_ranges(maps:get(extended_pictographic,GBP)),
    %% Pick codepoints below 256 (some data knowledge here)
    {ExtendedPictographicLow,_ExtendedPictographicHigh} =
        lists:splitwith(fun({Start,undefined}) -> Start < 256 end,ExtendedPictographic0),
    [GenExtP(CP) || CP <- merge_ranges(ExtendedPictographicLow)],

    io:put_chars(Fd,
                 "gc_1(R0,CP) when ?IS_LATIN1(CP) ->\n"
                 "    case R0 of\n"
                 "        [CP2|_] when ?IS_LATIN1(CP2) -> [CP|R0];\n"
                 "        _ -> gc_extend(cp(R0), R0, CP)\n"
                 "    end;\n"
                 "gc_1(_, CP) when not ?IS_CP(CP) ->\n"
                 "    error({badarg,CP});\n"),
    io:put_chars(Fd, "\n%% Continue control\n"),
    [GenControl(CP) || CP <- merge_ranges(Crs)],

    io:put_chars(Fd, "\n%% Handle prepend\n"),
    GenPrepend = fun(Range) -> io:format(Fd, "gc_1~s gc_prepend(R0, CP);\n", [gen_clause(Range)]) end,
    [GenPrepend(CP) || CP <- merge_ranges(maps:get(prepend,GBP))],

    io:put_chars(Fd, "\n%% Handle Hangul L\n"),
    GenHangulL = fun(Range) -> io:format(Fd, "gc_1~s gc_h_L(R0,[CP]);\n", [gen_clause(Range)]) end,
    [GenHangulL(CP) || CP <- merge_ranges(maps:get(l,GBP))],
    io:put_chars(Fd, "%% Handle Hangul V\n"),
    GenHangulV = fun(Range) -> io:format(Fd, "gc_1~s gc_h_V(R0,[CP]);\n", [gen_clause(Range)]) end,
    [GenHangulV(CP) || CP <- merge_ranges(maps:get(v,GBP))],
    io:put_chars(Fd, "%% Handle Hangul T\n"),
    GenHangulT = fun(Range) -> io:format(Fd, "gc_1~s gc_h_T(R0,[CP]);\n", [gen_clause(Range)]) end,
    [GenHangulT(CP) || CP <- merge_ranges(maps:get(t,GBP))],
    io:put_chars(Fd, "%% Handle Hangul LV and LVT special, since they are large\n"),
    io:put_chars(Fd, "gc_1(R0,CP) when is_integer(CP, 44000, 56000) -> R=[CP|R0], gc_h_lv_lvt(R, R, []);\n"),

    io:put_chars(Fd, "\n%% Handle Regional\n"),
    GenRegional = fun(Range) -> io:format(Fd, "gc_1~s gc_regional(R0,CP);\n", [gen_clause(Range)]) end,
    [GenRegional(CP) || CP <- merge_ranges(maps:get(regional_indicator,GBP))],
    %% io:put_chars(Fd, "%% Handle E_Base\n"),
    %% GenEBase = fun(Range) -> io:format(Fd, "gc_1~s gc_e_cont(R1,[CP]);\n", [gen_clause(Range)]) end,
    %% [GenEBase(CP) || CP <- merge_ranges(maps:get(e_base,GBP))],
    %% io:put_chars(Fd, "%% Handle EBG\n"),
    %% GenEBG = fun(Range) -> io:format(Fd, "gc_1~s gc_e_cont(R1,[CP]);\n", [gen_clause(Range)]) end,
    %% [GenEBG(CP) || CP <- merge_ranges(maps:get(e_base_gaz,GBP))],

    %% io:put_chars(Fd, "\n%% Handle extended_pictographic\n"),
    %% [GenExtP(CP) || CP <- merge_ranges(ExtendedPictographicHigh)],

    io:put_chars(Fd, "\n%% default clauses\n"),
    io:put_chars(Fd, """
 gc_1(R,CP) ->
     case is_ext_pict(CP) of
         true -> gc_ext_pict(R, [CP]);
         false ->
             case is_indic_consonant(CP) of
                 true -> gc_indic(cp(R), R, false, [CP]);
                 false -> gc_extend(cp(R), R, CP)
             end
     end.


 """),

    io:put_chars(Fd, "%% Handle Prepend\n"),
    io:put_chars(Fd,
                 "gc_prepend(R00, CP0) ->\n"
                 "    case cp(R00) of\n"
                 "      [CP1|R0] ->\n"
                 "          case is_control(CP1) of\n"
                 "            true -> [CP0|R00];\n"
                 "            false ->\n"
                 "                case gc_1(R0, CP1) of\n"
                 "                    [GC|R1] when is_integer(GC) -> [[CP0,GC]|R1];\n"
                 "                    [GC|R1] -> [[CP0|GC]|R1]\n"
                 "                end\n"
                 "           end;\n"
                 "      [] -> [CP0];\n"
                 "      {error,R} -> [CP0|R]\n"
                 "    end.\n\n"),

    IsCtrl = fun(Range) -> io:format(Fd, "is_control~s true;\n", [gen_single_clause(Range)]) end,
    [IsCtrl(CP) || CP <- merge_ranges(maps:get(cr, GBP) ++ maps:get(lf, GBP) ++ maps:get(control, GBP))],
    io:put_chars(Fd, "is_control(_) -> false.\n\n"),

    io:put_chars(Fd, "%% Handle Extend\n"
                 "%% To simplify binary handling in libraries the tail should be kept binary\n"
                 "%% and not a lookahead CP\n"
                ),
    io:put_chars(Fd,
                 "gc_extend([CP|T], T0, CP0) ->\n"
                 "    case is_extend(CP) of\n"
                 "        false -> [CP0|T0]; % losing work done on T\n"
                 "        _TrueOrZWJ -> gc_extend2(cp(T), T, [CP,CP0])\n"
                 "    end;\n"
                 "gc_extend([], T0, CP) -> [CP|T0];\n"
                 "gc_extend({error,R}, _, CP) -> [CP|R].\n\n"),
    io:put_chars(Fd,
                 "gc_extend2([CP|T], T0, Acc) ->\n"
                 "    case is_extend(CP) of\n"
                 "        false -> [lists:reverse(Acc)|T0]; % losing work done on T\n"
                 "        _TrueOrZWJ -> gc_extend2(cp(T), T, [CP|Acc])\n"
                 "    end;\n"
                 "gc_extend2([], T0, Acc) ->\n"
                 "    [lists:reverse(Acc)|T0];\n"
                 "gc_extend2({error,R}, _, Acc) ->\n"
                 "    [lists:reverse(Acc)] ++ [R].\n\n"
                 ),
    [{ZWJ, undefined}=ZWJRange] = maps:get(zwj, GBP),
    GenExtend = fun(R) when R =:= ZWJRange -> ok;
                   (Range) -> io:format(Fd, "is_extend~s true;\n", [gen_single_clause(Range)])
                end,
    io:format(Fd, "is_extend(~w) -> zwj;\n", [ZWJ]),
    Extends = merge_ranges(maps:get(extend,GBP)++maps:get(spacingmark, GBP), true),
    [GenExtend(CP) || CP <- Extends],
    io:put_chars(Fd, "is_extend(_) -> false.\n\n"),

    io:put_chars(Fd,
                 "gc_ext_pict(T, Acc) ->\n"
                 "    gc_ext_pict(cp(T), T, Acc).\n\n"
                 "gc_ext_pict([CP|R1], T0, Acc) ->\n"
                 "    case is_extend(CP) of\n"
                 "        zwj -> gc_ext_pict_zwj(cp(R1), R1, [CP|Acc]);\n"
                 "        true -> gc_ext_pict(R1, [CP|Acc]);\n"
                 "        false -> add_acc(Acc, T0)\n"
                 "    end;\n"
                 "gc_ext_pict([], T0, Acc) ->\n"
                 "    add_acc(Acc, T0);\n"
                 "gc_ext_pict({error,R}, T, Acc) ->\n"
                 "    gc_ext_pict([], T, Acc) ++ [R].\n\n"),
    io:put_chars(Fd,
                 "gc_ext_pict_zwj([CP|R1], T0, Acc) ->\n"
                 "    case is_ext_pict(CP) of\n"
                 "        true -> gc_ext_pict(R1, [CP|Acc]);\n"
                 "        false -> add_acc(Acc, T0)\n"
                 "    end;\n"
                 "gc_ext_pict_zwj([], T0, Acc) ->\n"
                 "    add_acc(Acc, T0);\n"
                 "gc_ext_pict_zwj({error,R}, T, Acc) ->\n"
                 "    gc_ext_pict_zwj([], T, Acc) ++ [R].\n\n"),

    GenExtPict = fun(Range) -> io:format(Fd, "is_ext_pict~s true;\n", [gen_single_clause(Range)]) end,
    [GenExtPict(CP) || CP <- ExtendedPictographic0],
    io:put_chars(Fd, "is_ext_pict(_) -> false.\n\n"),

    %% --------------------
    io:put_chars(Fd, "%% Handle Regional\n"),
    [{RLess,RLarge}] = merge_ranges(maps:get(regional_indicator,GBP)),
    io:put_chars(Fd,"gc_regional(R0, CP0) ->\n"
                 "    case cp(R0) of\n"),
    io:format(Fd,   "        [CP|R1] when is_integer(CP, ~w, ~w) ->\n"
              "             gc_extend2(cp(R1),R1,[CP,CP0]);~n",
              [RLess, RLarge]),
    io:put_chars(Fd,"        R1 -> gc_extend(R1, R0, CP0)\n"
                 "    end.\n\n"),

    %% Special hangul
    io:put_chars(Fd, "%% Handle Hangul L\n"),
    io:put_chars(Fd, "gc_h_L(R0, Acc) ->\n    case cp(R0) of\n"),
    GenHangulL_1 = fun(Range) -> io:format(Fd, "~8c~s gc_h_L(R1,[CP|Acc]);\n",
                                           [$\s,gen_case_clause(Range)]) end,
    [GenHangulL_1(CP) || CP <- merge_ranges(maps:get(l,GBP))],
    GenHangulL_2 = fun(Range) -> io:format(Fd, "~8c~s gc_h_V(R1,[CP|Acc]);\n",
                                           [$\s,gen_case_clause(Range)]) end,
    [GenHangulL_2(CP) || CP <- merge_ranges(maps:get(v,GBP))],
    io:put_chars(Fd, "        R1 -> gc_h_lv_lvt(R1, R0, Acc)\n    end.\n\n"),

    io:put_chars(Fd, "%% Handle Hangul V\n"),
    io:put_chars(Fd, "gc_h_V(R0, Acc) ->\n    case cp(R0) of\n"),
    GenHangulV_1 = fun(Range) -> io:format(Fd, "~8c~s gc_h_V(R1,[CP|Acc]);\n",
                                           [$\s,gen_case_clause(Range)]) end,
    [GenHangulV_1(CP) || CP <- merge_ranges(maps:get(v,GBP))],
    GenHangulV_2 = fun(Range) -> io:format(Fd, "~8c~s gc_h_T(R1,[CP|Acc]);\n",
                                           [$\s,gen_case_clause(Range)]) end,
    [GenHangulV_2(CP) || CP <- merge_ranges(maps:get(t,GBP))],
    io:put_chars(Fd,
                 "        R1 ->\n"
                 "            case Acc of\n"
                 "                [CP] -> gc_extend(R1, R0, CP);\n"
                 "                _ -> gc_extend2(R1, R0, Acc)\n"
                 "            end\n    end.\n\n"),
    io:put_chars(Fd, "%% Handle Hangul T\n"),
    io:put_chars(Fd, "gc_h_T(R0, Acc) ->\n    case cp(R0) of\n"),
    GenHangulT_1 = fun(Range) -> io:format(Fd, "~8c~s gc_h_T(R1,[CP|Acc]);\n",
                                           [$\s,gen_case_clause(Range)]) end,
    [GenHangulT_1(CP) || CP <- merge_ranges(maps:get(t,GBP))],
    io:put_chars(Fd,
                 "        R1 ->\n"
                 "            case Acc of\n"
                 "                [CP] -> gc_extend(R1, R0, CP);\n"
                 "                _ -> gc_extend2(R1, R0, Acc)\n"
                 "            end\n    end.\n\n"),
    io:put_chars(Fd, "%% Handle Hangul LV\n"),
    io:put_chars(Fd, "gc_h_lv_lvt([CP|_], _R0, _Acc) when not ?IS_CP(CP) -> error(badarg);\n"),
    GenHangulLV = fun(Range) -> io:format(Fd, "gc_h_lv_lvt~s gc_h_V(R1,[CP|Acc]);\n",
                                          [gen_clause2(Range)]) end,
    [GenHangulLV(CP) || CP <- merge_ranges(maps:get(lv,GBP))],
    io:put_chars(Fd, "%% Handle Hangul LVT\n"),
    GenHangulLVT = fun(Range) -> io:format(Fd, "gc_h_lv_lvt~s gc_h_T(R1,[CP|Acc]);\n",
                                           [gen_clause2(Range)]) end,
    [GenHangulLVT(CP) || CP <- merge_ranges(maps:get(lvt,GBP))],
    io:put_chars(Fd, "gc_h_lv_lvt([CP|R1], _, []) -> gc_extend(cp(R1), R1, CP);\n"), %% From gc_1/1
    io:put_chars(Fd, "%% Also handles error tuples\n"),
    io:put_chars(Fd, "gc_h_lv_lvt(R1, R0, [CP]) -> gc_extend(R1, R0, CP);\n"),
    io:put_chars(Fd, "gc_h_lv_lvt(R1, R0, Acc) -> gc_extend2(R1, R0, Acc).\n\n"),

    %% Indic
    %% See tr44 5.3.1 Derivation of Indic_Conjunct_Break
    %%  and IndicSyllabicCategory.txt  also tr29 conjunctCluster
    io:put_chars(Fd, "\n%% Handle Indic Conjunct Break\n"),
    Consonants = maps:get(consonant, GBP) ++
        maps:get(vowel_independent, GBP) ++
        [{16#1B0B, 16#1B0C}],
    GenIndicC = fun(Range) -> io:format(Fd, "is_indic_consonant~s true;\n", [gen_single_clause(Range)]) end,
    [GenIndicC(CP) || CP <- merge_ranges(Consonants)],
    io:format(Fd, "is_indic_consonant(_) -> false.\n\n", []),

    GenIndicL = fun(Range) -> io:format(Fd, "is_indic_linker~s true;\n", [gen_single_clause(Range)]) end,
    Linkers = maps:get(virama, GBP) ++ maps:get(invisible_stacker, GBP),
    [GenIndicL(CP) || CP <- merge_ranges(Linkers)],
    io:format(Fd, "is_indic_linker(_) -> false.\n\n", []),

    io:put_chars(Fd,
                 """
                 gc_indic([CP|R1], R0, FetchedLinker, CPs) ->
                     case is_indic_linker(CP) of
                         true ->
                             gc_indic(cp(R1), R1, true, [CP|CPs]);
                         false ->
                             case is_extend(CP) of
                                 false when FetchedLinker ->
                                     case is_indic_consonant(CP) of
                                         true -> gc_indic(cp(R1), R1, false, [CP|CPs]);
                                         false -> add_acc(CPs, R0)
                                     end;
                                 false ->
                                     add_acc(CPs, R0);
                                 _ ->
                                     gc_indic(cp(R1), R1, FetchedLinker, [CP|CPs])
                             end
                     end;
                 gc_indic([], R0, _, CPs) ->
                     add_acc(CPs, R0);
                 gc_indic({error, R0}, _, _, CPs) ->
                     add_acc(CPs, R0).

                 add_acc([CP], R) -> [CP|R];
                 add_acc(CPs, R) -> [lists:reverse(CPs)|R].


                 """),

    ok.

gen_compose_pairs(Fd, ExclData, Data) ->
    GetPairs =
        fun(CP, #cp{dec=[DCP,_]=Pair}, Acc) ->
                case array:get(DCP,Data) of
                    #cp{class=0} -> [{Pair, CP}|Acc];
                    #cp{} -> Acc %% Ignore Non-Starter Decompositions
                end;
           (_, _, Acc) -> Acc
        end,
    DeCmp2 = array:sparse_foldl(GetPairs, [], ExclData),
    [io:format(Fd, "compose_pair(~w,~w) -> ~w;~n", [A,B,CP]) || {[A,B],CP} <- lists:sort(DeCmp2)],
    io:put_chars(Fd, "compose_pair(_,_) -> false.\n\n"),

    io:put_chars(Fd, "nolist(CP, []) when ?IS_CP(CP) -> CP;\n"
                 "nolist(CP, L) when ?IS_CP(CP) -> [CP|L].\n\n"),
    ok.

gen_case_table(Fd, Data) ->
    HC = fun(CP, #cp{cs=Cs}, Acc) ->
                 case case_data(CP, Cs) of
                     default -> Acc;
                     CaseData -> [{CP,CaseData}|Acc]
                 end
         end,
    Case = array:sparse_foldr(HC, [], Data),
    [io:format(Fd, "case_table(~w) -> ~w;\n", [CP, Map])|| {CP,Map} <- Case],
    io:format(Fd, "case_table(CP) -> {CP, CP}.\n\n",[]),
    ok.

case_data(CP, {U0,L0,T0,F0}) ->
    U = def_cp(U0,CP),
    L = def_cp(L0,CP),
    T = def_cp(T0,CP),
    F = def_cp(F0,CP),
    case T =:= U andalso F =:= L of
        true  -> {U,L};
        false -> {U,L,T,F}
    end;
case_data(_, _) ->
    default.

def_cp([], CP) -> CP;
def_cp(CP, _) -> CP.

gen_unicode_table(Fd, Data, UpdateTests) ->
    FixCanon = fun(_, #cp{class=CCC, dec=Dec, comp=Comp, cat=Cat}) ->
                       Canon  = decompose(Dec,Data),
                       #{ccc=>CCC, canonical=>Canon, compat=>Comp, cat=>Cat}
               end,
    AofMaps0 = array:sparse_map(FixCanon, Data),
    FixCompat = fun(_, #{ccc:=CCC, canonical:=Canon, compat:=Comp, cat:=Cat}) ->
                        Compat = decompose_compat(Canon, Comp, AofMaps0),
                        {CCC, Canon, Compat, category(Cat)}
                end,
    AofMaps1 = array:sparse_map(FixCompat, AofMaps0),

    Dict0 = array:sparse_to_orddict(AofMaps1),
    Def = {0, [], [], lookup_category},
    {NonDef, CatTable} = lists:partition(fun({_, {0,[],[],_Cat}}) -> false;
                                            (_) -> true
                                         end, Dict0),

    %% Export testfile
    case UpdateTests of
        true ->
            Dict1 = lists:map(fun({Id,{CCC, Canon, Compat, Cat}}) ->
                                      {Id, {CCC, Canon, Compat, Cat}}
                              end, Dict0),
            TestFile = "../test/unicode_util_SUITE_data/unicode_table.bin",
            io:format("Updating: ~s~n", [TestFile]),
            file:write_file(TestFile, term_to_binary(Dict1, [compressed]));
        false ->
            ignore
    end,

    [io:format(Fd, "unicode_table(~w) -> ~w;~n", [CP, Map]) || {CP,Map} <- NonDef],
    io:format(Fd, "unicode_table(_) -> ~w.~n~n",[Def]),

    %% [io:format(Fd, "cat_translate(~w) -> ~w;~n", [Cat, EC]) || {Cat,EC} <- category_translate()],
    %% io:format(Fd, "cat_translate(Cat) -> error({internal_error, Cat}).~n~n",[]),
    gen_category(Fd, CatTable, Data),
    ok.

category([C,Sub]) ->
    Map = category_translate(),
    maps:get(list_to_atom([C-$A+$a, Sub]), Map).

category_translate() ->
    #{lu => {letter, uppercase},       % Letter, Uppercase
      ll => {letter, lowercase},       % Letter, Lowercase
      lt => {letter, titlecase},       % Letter, Titlecase
      mn => {mark, non_spacing},       % Mark, Non-Spacing
      mc => {mark, spacing_combining}, % Mark, Spacing Combining
      me => {mark, enclosing},         % Mark, Enclosing
      nd => {number, decimal},         % Number, Decimal Digit
      nl => {number, letter},          % Number, Letter
      no => {number, other},           % Number, Other
      zs => {separator, space},        % Separator, Space
      zl => {separator, line},         % Separator, Line
      zp => {separator, paragraph},    % Separator, Paragraph
      cc => {other, control},          % Other, Control
      cf => {other, format},           % Other, Format
      cs => {other, surrogate},        % Other, Surrogate
      co => {other, private},          % Other, Private Use
      cn => {other, not_assigned},     % Other, Not Assigned (no characters in the file have this property)
      lm => {letter, modifier},        % Letter, Modifier
      lo => {letter, other},           % Letter, Other
      pc => {punctuation, connector},  % Punctuation, Connector
      pd => {punctuation, dash},       % Punctuation, Dash
      ps => {punctuation, open},       % Punctuation, Open
      pe => {punctuation, close},      % Punctuation, Close
      pi => {punctuation, initial},    % Punctuation, Initial quote (may behave like Ps or Pe depending on usage)
      pf => {punctuation, final},      % Punctuation, Final quote (may behave like Ps or Pe depending on usage)
      po => {punctuation, other},      % Punctuation, Other
      sm => {symbol, math},            % Symbol, Math
      sc => {symbol, currency},        % Symbol, Currency
      sk => {symbol, modifier},        % Symbol, Modifier
      so => {symbol, other}            % Symbol, Other
     }.

gen_category(Fd, [{CP, {_, _, _, Cat}}|Rest], All) ->
    {Single, Range, SubCat} = gen_category(Rest, Cat, CP, CP, All, [], [], []),
    [io:format(Fd, "lookup_category(~w) -> ~w;~n", [X, C]) || {X,C} <:- Single],

    Fun = fun(subcat) -> "subcat_letter(CP)";
             (Category) -> io_lib:format("~w", [Category])
          end,
    [io:format(Fd, "lookup_category(CP) when is_integer(CP, ~w, ~w) -> ~s;~n",
               [S, E, Fun(C)]) || {S,E,C} <:- optimize_ranges_1(Range)],
    io:put_chars(Fd, "lookup_category(Cp) -> {other, not_assigned}.\n\n"),

    {SubSingle, SubRange} = gen_letter(SubCat, All),
    [io:format(Fd, "subcat_letter(~w) -> ~w;~n", [X, C]) || {X,C} <:- SubSingle],
    [io:format(Fd, "subcat_letter(CP) when is_integer(CP, ~w, ~w) -> ~w;~n",
               [S, E, C]) || {S,E,C} <:- optimize_ranges_1(SubRange)],
    io:put_chars(Fd,
                 "subcat_letter(CP) ->\n"
                 "    case case_table(CP) of\n"
                 "        {CP, CP} -> {letter,other};\n"
                 "        {CP, _}  -> {letter,uppercase};\n"
                 "        {_, CP}  -> {letter,lowercase};\n"
                 "        {_, _, CP, _} -> {letter,titlecase};\n"
                 "        {CP, _, _, _} -> {letter,uppercase};\n"
                 "        {_,CP,_,_} -> {letter,lowercase}\n"
                 "    end.\n\n"),
    ok.

gen_category([{CP, {_, _, _, NextCat}}|Rest], Cat, Start, End, All, Single, Range, SubCats)
  when End+1 =:= CP ->
    IsLetterCat = letter_cat(NextCat, Cat),
    if NextCat =:= Cat ->
            gen_category(Rest, Cat, Start, CP, All, Single, Range, SubCats);
       IsLetterCat ->
            gen_category(Rest, letter, Start, CP, All, Single, Range, SubCats);
       Start =:= End ->
            gen_category(Rest, NextCat, CP, CP, All, [{Start, Cat}|Single], Range, SubCats);
       true ->
            case Cat of
                letter ->
                    gen_category(Rest, NextCat, CP, CP, All,
                                 Single, [{Start, End, subcat}|Range],
                                 lists:reverse(lists:seq(Start, End)) ++ SubCats);
                _ ->
                    gen_category(Rest, NextCat, CP, CP, All,
                                 Single, [{Start, End, Cat}|Range], SubCats)
            end
    end;
gen_category([{CP, {_, _, _, NewCat}}|Rest]=Cont, Cat, Start, End, All, Single, Range, SubCats) ->
    case array:get(End+1, All) of
        undefined ->
            if Start =:= End ->
                    gen_category(Rest, NewCat, CP, CP, All,
                                 [{Start, Cat}|Single], Range, SubCats);
               true ->
                    case Cat of
                        letter ->
                            gen_category(Rest, NewCat, CP, CP, All,
                                         Single, [{Start, End, subcat}|Range],
                                         lists:reverse(lists:seq(Start, End)) ++ SubCats);
                        _ ->
                            gen_category(Rest, NewCat, CP, CP, All,
                                         Single, [{Start, End, Cat}|Range], SubCats)
                    end
            end;
        _ ->  %% We can make ranges larger by setting already assigned category
            gen_category(Cont, Cat, Start, End+1, All, Single, Range, SubCats)
    end;
gen_category([], Cat, Start, End, _All, Single, Range, SubCats) ->
    case Start =:= End of
        true ->
            {lists:reverse([{Start, Cat}|Single]),
             lists:reverse(Range),
             lists:reverse(SubCats)};
        false ->
            {lists:reverse(Single),
             lists:reverse([{Start, End,Cat}|Range]),
             lists:reverse(SubCats)}
    end.

letter_cat(lm, _) ->
    false;
letter_cat(_, lm) ->
    false;
letter_cat(L1, L2) ->
    is_letter(L1) andalso (L2 =:= letter orelse is_letter(L2)).

is_letter({letter, _}) -> true;
is_letter(_) -> false.

gen_letter(Letters, All) ->
    gen_letter(Letters, All, []).
gen_letter([CP|Rest], All, Acc) ->
    case array:get(CP, All) of
        undefined ->
            gen_letter(Rest, All, Acc);
        #cp{cat=Cat0, cs=Cs} ->
            case {category(Cat0), case_table(CP,case_data(CP, Cs))} of
                {Sub,Sub} ->
                    gen_letter(Rest, All, Acc);
                {lm,_} ->
                    gen_letter(Rest, All, Acc);
                {Cat, _Dbg} ->
                    case is_letter(Cat) of
                        true ->
                            gen_letter(Rest, All, [{CP, Cat}|Acc]);
                        false ->
                            gen_letter(Rest, All, Acc)
                    end
            end
    end;
gen_letter([], _, Acc) ->
    [{Start, Cat}|SCletters] = lists:reverse(Acc),
    subcat_letter(SCletters, Start, Start, Cat, [], []).

subcat_letter([{CP, Cat}|R], Start, End, Cat, Single, Range) when End+1 =:= CP ->
    subcat_letter(R, Start, CP, Cat, Single, Range);
subcat_letter([{CP, NewCat}|R], Start, Start, Cat, Single, Range) ->
    subcat_letter(R, CP, CP, NewCat, [{Start, Cat}|Single], Range);
subcat_letter([{CP, NewCat}|R], Start, End, Cat, Single, Range) ->
    subcat_letter(R, CP, CP, NewCat, Single, [{Start, End, Cat}|Range]);
subcat_letter([], Start, End, Cat, Single, Range) ->
    case Start == End of
        true -> {lists:reverse([{Start, Cat}|Single]), lists:reverse(Range)};
        false -> {lists:reverse(Single), lists:reverse([{Start, End, Cat}|Range])}
    end.


case_table(CP, CaseData) ->
    case CaseData of
        {CP, CP} -> lo;
        {CP, _}  -> lu;
        {_, CP}  -> ll;
        {_, _, CP, _} -> lt;
        {CP, _, _, _} -> lu;
        {_,CP,_,_} -> ll;
        default -> lo
    end.

decompose([], _Data) -> [];
decompose([CP|CPs], Data) when is_integer(CP) ->
    case array:get(CP,Data) of
        undefined -> [{0,CP}|decompose(CPs,Data)];
        #cp{class=CCC, dec=[]} -> [{CCC,CP}|decompose(CPs,Data)];
        #cp{dec=Dec} -> decompose(Dec, Data) ++ decompose(CPs,Data)
    end.

decompose_compat(Canon, [], Data) ->
    case decompose_compat(Canon, Data) of
        Canon -> [];
        Other -> {compat, Other}
    end;
decompose_compat([], {CompatMode, CPs}, Data) ->
    {CompatMode, decompose_compat(CPs, Data)}.

decompose_compat([], _Data) -> [];
decompose_compat([CP|CPs], Data) when is_integer(CP) ->
    case array:get(CP,Data) of
        undefined ->
            [{0,CP}|decompose_compat(CPs,Data)];
        #{ccc:=CCC, canonical:=[], compat:=[]} ->
            [{CCC,CP}|decompose_compat(CPs,Data)];
        #{canonical:=[], compat:={_,Dec}} ->
            decompose_compat(Dec, Data) ++ decompose_compat(CPs,Data);
        #{canonical:=Dec} ->
            decompose_compat(Dec, Data) ++ decompose_compat(CPs,Data)
    end;
decompose_compat([{_,CP}|CPs], Data) ->
    decompose_compat([CP|CPs], Data).


gen_width_table(Fd, WideChars) ->
    MergedWCs = merge_ranges(WideChars),
    Write = fun(Range) -> io:format(Fd, "is_wide_cp~s true;~n", [gen_single_clause(Range)]) end,
    [Write(Range) || Range <- MergedWCs],
    io:format(Fd, "is_wide_cp(_) -> false.~n", []).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

gen_clause({R0, undefined}) ->
    io_lib:format("(R0, ~w=CP) ->", [R0]);
gen_clause({R0, R1}) ->
    io_lib:format("(R0, CP) when is_integer(CP, ~w, ~w) ->", [R0,R1]).

gen_clause2({R0, undefined}) ->
    io_lib:format("([~w=CP|R1], R0, Acc) ->", [R0]);
gen_clause2({R0, R1}) ->
    io_lib:format("([CP|R1], R0, Acc) when is_integer(CP, ~w, ~w) ->", [R0,R1]).

gen_case_clause({R0, undefined}) ->
    io_lib:format("[~w=CP|R1] ->", [R0]);
gen_case_clause({R0, R1}) ->
    io_lib:format("[CP|R1] when is_integer(CP, ~w, ~w) ->", [R0,R1]).


gen_single_clause({R0, undefined}) ->
    io_lib:format("(~w) ->", [R0]);
gen_single_clause({R0, R1}) ->
    io_lib:format("(CP) when is_integer(CP, ~w, ~w) ->", [R0,R1]).


merge_ranges(List) ->
    merge_ranges(List, true).

merge_ranges(List, Opt) ->
    Res0 = merge_ranges_1(lists:usort(List), []),
    case Opt of
        split ->
            split_ranges(Res0,[]);     % One clause per CP
        true ->
            Res = split_small_ranges(Res0, []),
            OptRes = optimize_ranges(Res),
            true = lists:sort(Res) =:= lists:sort(OptRes), %Assertion.
            OptRes;
        false ->
            Res0
    end.

merge_ranges_1([{Next, Stop}|R], [{Start,undefined}|Acc]) when Start+1 =:= Next ->
    case Stop of
        undefined -> merge_ranges_1(R, [{Start, Next}|Acc]);
        _ -> merge_ranges_1(R, [{Start,Stop}|Acc])
    end;
merge_ranges_1([{Next, Stop}|R], [{Start,Prev}|Acc]) when Prev+1 =:= Next ->
    case Stop of
        undefined -> merge_ranges_1(R, [{Start, Next}|Acc]);
        _ -> merge_ranges_1(R, [{Start,Stop}|Acc])
    end;
merge_ranges_1([Next|R], Acc) ->
    merge_ranges_1(R, [Next|Acc]);
merge_ranges_1([], Acc) ->
    lists:reverse(Acc).

split_ranges([{_,undefined}=CP|Rs], Acc) ->
    split_ranges(Rs,[CP|Acc]);
split_ranges([{F,L}|Rs], Acc) when F < L ->
    split_ranges([{F+1,L}|Rs],[{F,undefined}|Acc]);
split_ranges([{L,L}|Rs], Acc) ->
    split_ranges(Rs,[{L, undefined}|Acc]);
split_ranges([], Acc) ->
    lists:reverse(Acc).

split_small_ranges([{_,undefined}=CP|Rs], Acc) ->
    split_small_ranges(Rs,[CP|Acc]);
split_small_ranges([{L,L}|Rs], Acc) ->
    split_small_ranges(Rs,[{L, undefined}|Acc]);
split_small_ranges([{F,L}=Range|Rs], Acc) ->
    case L - F of
        1 ->
            split_small_ranges(Rs, [{L, undefined}, {F, undefined}|Acc]);
        N when N > 1 ->
            split_small_ranges(Rs, [Range|Acc])
    end;
split_small_ranges([], Acc) ->
    lists:reverse(Acc).


optimize_ranges(Rs0) ->
    PF = fun({N,undefined}) when is_integer(N) -> true;
            (_) -> false
         end,
    {Singles,Rs} = lists:partition(PF, Rs0),
    Singles ++ optimize_ranges_1(Rs).

optimize_ranges_1(Rs) ->
    case length(Rs) of
        N when N >= 4 ->
            {R0,[Mid|R1]} = lists:split(N div 2, Rs),
            [Mid|optimize_ranges_1(R0)++optimize_ranges_1(R1)];
        _ ->
            Rs
    end.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

hex_to_int([]) -> [];
hex_to_int(HexStr) ->
    list_to_integer(string:trim(HexStr, both), 16).

to_atom(Str) ->
    list_to_atom(string:lowercase(string:trim(Str, both))).

foldl(Fun, Acc, Fd) ->
    Get = fun() -> file:read_line(Fd) end,
    foldl_1(Fun, Acc, Get).

foldl_1(_Fun, {done, Acc}, _Get) -> Acc;
foldl_1(Fun, Acc, Get) ->
    case Get() of
        eof -> Acc;
        {ok, "#" ++ _} -> %% Ignore comments
            foldl_1(Fun, Acc, Get);
        {ok, "\n"} -> %% Ignore empty lines
            foldl_1(Fun, Acc, Get);
        {ok, Line} ->
            foldl_1(Fun, Fun(Line, Acc), Get)
    end.



%% Differs from string:lexemes, it returns empty string as token between two delimiters
tokens(S, [C]) ->
    tokens(lists:reverse(S), C, []).

tokens([Sep|S], Sep, Toks) ->
    tokens(S, Sep, [[]|Toks]);
tokens([C|S], Sep, Toks) ->
    tokens_2(S, Sep, Toks, [C]);
tokens([], _, Toks) ->
    Toks.

tokens_2([Sep|S], Sep, Toks, Tok) ->
    tokens(S, Sep, [Tok|Toks]);
tokens_2([C|S], Sep, Toks, Tok) ->
    tokens_2(S, Sep, Toks, [C|Tok]);
tokens_2([], _Sep, Toks, Tok) ->
    [Tok|Toks].
